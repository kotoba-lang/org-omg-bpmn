(ns bpmn.bounded-matrix-test
  (:require [clojure.test :refer [deftest is testing]]
            [bpmn.model :as m]
            [bpmn.validate :as v]))

(def node-types
  {0 :start-event, 1 :end-event, 2 :exclusive-gateway,
   3 :inclusive-gateway, 4 :task})

(defn decode-model [encoded]
  (let [node-count (encoded 0)
        flow-count (encoded 1)
        flow-base (+ 2 (* node-count 3))
        with-nodes
        (reduce
         (fn [process index]
           (let [offset (+ 2 (* index 3))
                 id (str (encoded offset))
                 type (node-types (encoded (inc offset)))
                 default-id (encoded (+ offset 2))]
             (m/add process type id
                    (cond-> {} (not= -1 default-id)
                      (assoc :default (str default-id))))))
         (m/process "P")
         (range node-count))]
    (reduce
     (fn [process index]
       (let [offset (+ flow-base (* index 4))
             id (str (encoded offset))
             source (str (encoded (inc offset)))
             target (str (encoded (+ offset 2)))
             conditioned? (= 1 (encoded (+ offset 3)))]
         (m/connect process source target
                    (cond-> {:id id} conditioned? (assoc :condition "x")))))
     with-nodes
     (range flow-count))))

(def cases
  [{:name "clean" :encoded [3 2, 1 0 -1, 2 4 -1, 3 1 -1,
                             10 1 2 0, 11 2 3 0]
    :errors 0 :warnings 0}
   {:name "dangling target" :encoded [2 2, 1 0 -1, 2 1 -1,
                                       10 1 99 0, 11 1 2 0]
    :errors 1 :warnings 0}
   {:name "single task" :encoded [1 0, 1 4 -1]
    :errors 0 :warnings 4}
   {:name "exclusive without default" :encoded [4 3, 1 0 -1, 2 2 -1,
                                                  3 1 -1, 4 1 -1,
                                                  10 1 2 0, 11 2 3 1, 12 2 4 0]
    :errors 0 :warnings 1}
   {:name "inclusive all conditioned" :encoded [4 3, 1 0 -1, 2 3 -1,
                                                  3 1 -1, 4 1 -1,
                                                  10 1 2 0, 11 2 3 1, 12 2 4 1]
    :errors 0 :warnings 1}
   {:name "inclusive with default" :encoded [4 3, 1 0 -1, 2 3 12,
                                               3 1 -1, 4 1 -1,
                                               10 1 2 0, 11 2 3 1, 12 2 4 0]
    :errors 0 :warnings 0}])

(deftest bounded-fixtures-agree-with-cljc-validator
  (doseq [{:keys [name encoded errors warnings]} cases]
    (testing name
      (let [problems (v/problems (decode-model encoded))]
        (is (= errors (count (filter #(= :error (:bpmn/severity %)) problems))))
        (is (= warnings (count (filter #(= :warn (:bpmn/severity %)) problems))))))))
